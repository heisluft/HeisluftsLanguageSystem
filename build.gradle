group 'de.heisluft'
version '1.0.0'

apply plugin: 'java'
apply plugin: 'maven-publish'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        url "http://repo.dmulloy2.net/content/groups/public/"
    }
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        exclude 'plugin.yml'
    }
    from(sourceSets.main.resources.srcDirs) {
        include 'plugin.yml'
        expand("version": project.version, "name": project.name)
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}


task copyToServer(type: Copy, dependsOn: jar) {
    def ts = System.getProperty("serverDir")
    if (ts != null) {
        ts.replace('\\', '/')
        ts += (!ts.endsWith('/') ? '/' : "").concat("plugins")
        from jar
        into ts
    }
}

publishing {
    def server = System.getProperty("maven")
    if(server != null) {
        repositories {
            maven {
                url server
            }
        }
        publications {
            testing(MavenPublication) {
                from components.java
                artifact sourcesJar
                artifact javadocJar
            }
        }
    }
}


publish.dependsOn jar, sourcesJar, javadocJar

dependencies {
    compileOnly('com.comphenix.protocol:ProtocolLib-API:4.4.0-SNAPSHOT') {
        transitive=false
    }
    compileOnly 'org.spigotmc:spigot:1.12.2-R0.1-SNAPSHOT'
}
